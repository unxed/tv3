program t_app;
{$I ../src/tvdefs.inc}
uses TvGeom, TvColors, TvCell, TvEvents, TvKeys, TvScreen, TvViews, TvWindow, TvMenus,
  TvSys, TvTimer, TvMem, TvApp;
{$I testlib.inc}

var
  TE: TEvent;
  TS: AnsiString;
  Save: ShortString;

{ the main block keeps the temporaries (dynamic arrays) until its end: measure in a function }
function PalLen(A: TApplication): Integer;
begin
  Result := Byte(A.GetPalette[0]);
end;

type
  { A descendant of the desktop overrides InitBackground (fpide does): the method must stay visible to descendants (a compile-time check). }
  TTestDeskTop = class(TDeskTop)
    procedure InitBackground; override;
  end;

  TTestApp = class(TApplication)
    Idles: Integer;
    procedure InitMenuBar; override;
    procedure Idle; override;
  end;

  { records the broadcasts it gets }
  TWatch = class(TView)
    SetChanged, TimerFired: Integer;
    LastTimer: Pointer;
    procedure HandleEvent(var Event: TEvent); override;
  end;

  { a dialog: Execute returns cmOK at once; its data is a Word }
  TDlg = class(TView)
    Value: Word;
    function Execute: Word; override;
    function DataSize: Integer; override;
    procedure GetData(var Rec); override;
    procedure SetData(var Rec); override;
  end;

procedure TTestDeskTop.InitBackground;
begin
  inherited InitBackground;
end;

procedure TTestApp.InitMenuBar;
var
  R: TRect;
begin
  R := GetExtent;
  R.B.Y := R.A.Y + 1;
  MenuBar := TMenuBar.Create(R, TMenu.Create(TMenuItem.Create('~F~ile', kbNoKey, TMenu.Create(TMenuItem.Create('E~x~it', cmQuit, kbAltX, hcNoContext, 'Alt-X', nil)), hcNoContext, nil)));
end;

procedure TTestApp.Idle;
begin
  Inc(Idles);
  inherited Idle;
end;

procedure TWatch.HandleEvent(var Event: TEvent);
begin
  inherited HandleEvent(Event);
  if Event.What = evBroadcast then
    case Event.Message.Command of
      cmCommandSetChanged: Inc(SetChanged);
      cmTimerExpired:
        begin
          Inc(TimerFired);
          LastTimer := Event.Message.InfoPtr;
        end;
    end;
end;

function TDlg.Execute: Word;
begin
  Value := 99;
  Result := cmOK;
end;

function TDlg.DataSize: Integer;
begin
  Result := SizeOf(Word);
end;

procedure TDlg.GetData(var Rec);
begin
  Word(Rec) := Value;
end;

procedure TDlg.SetData(var Rec);
begin
  Value := Word(Rec);
end;

function R(A, B, C, D: Integer): TRect;
begin
  Result := TRect.Create(A, B, C, D);
end;

{ handles the queued events, as the loop of Run does }
procedure Pump(App: TTestApp);
var
  E: TEvent;
begin
  while MemPending > 0 do
  begin
    App.GetEvent(E);
    if E.What <> evNothing then
      App.HandleEvent(E);
  end;
end;

procedure Resize(Mode: Word);
begin
  ScreenCreate(80, 30);
end;

function NewWin(Num: Integer; X, Y: Integer): TWindow;
var
  W: TWindow;
begin
  W := TWindow.Create(R(X, Y, X + 20, Y + 8), 'Win', Num);
  W.Options := W.Options or ofTileable;
  Result := W;
end;

var
  App: TTestApp;
  Used0: PtrUInt;
  W1, W2, W3: TWindow;
  Watch: TWatch;
  Dlg: TDlg;
  Ev: TEvent;
  Id: TTimerId;
  I, D: Integer;
  Rc: TRect;
  Res: Word;
  Ys: array[0..2] of Integer;

begin
  Used0 := GetFPCHeapStatus.CurrHeapUsed;
  MemInit(60, 25);
  Check(TProgram.Application = nil, 'no application yet');
  App := TTestApp.Create;
  Check(TProgram.Application = App, 'Application is set');
  Check((TProgram.DeskTop <> nil) and (TProgram.MenuBar <> nil) and (TProgram.StatusLine <> nil), 'desktop, menu bar and status line exist');
  Check((App.State and (sfVisible or sfSelected or sfFocused or sfModal or sfExposed)) =
    (sfVisible or sfSelected or sfFocused or sfModal or sfExposed), 'the program is the modal top view');
  Check(App.Buffer = TScreen.ScreenBuffer, 'the program draws into the screen buffer');
  Check((TProgram.DeskTop.Origin.Y = 1) and (TProgram.DeskTop.Size.Y = 23) and (TProgram.DeskTop.Size.X = 60), 'desktop bounds');
  Check((TProgram.MenuBar.Origin.Y = 0) and (TProgram.MenuBar.Size.Y = 1), 'menu bar bounds');
  Check((TProgram.StatusLine.Origin.Y = 24) and (TProgram.StatusLine.Size.Y = 1), 'status line bounds');
  Check(TProgram.DeskTop.Background <> nil, 'the desktop has a background');
  Check(MemText(0, 0, 5) = '  File', 'menu bar text');
  Check(MemText(24, 0, 11) = ' Alt-X Exit ', 'status line text');
  Check(MemChar(0, 1) = '░', 'background pattern');
  { what a locked program lost is drawn again at Unlock (the buffer of the program is the screen: Draw of the group repaints
    the subviews, not the buffer onto itself) }
  FillChar(TScreen.ScreenBuffer^, TScreen.ScreenWidth * TScreen.ScreenHeight * SizeOf(TScreenCell), 0);
  App.Lock;
  App.Unlock;
  Check((MemText(0, 0, 5) = '  File') and (MemChar(0, 1) = '░'), 'Unlock of the program repaints it');
  Check(MemChar(59, 23) = '░', 'background fills the desktop');
  Check(MemAttr(0, 1) = $71, 'application palette: background');
  Check(MemAttr(0, 0) = $70, 'application palette: menu bar');
  Check(MemAttr(1, 24) = $74, 'application palette: status line hot key');
  Check(TProgram.AppPalette = apColor, 'color palette');

  { windows }
  W1 := App.InsertWindow(NewWin(1, 2, 2));
  Check(W1 <> nil, 'InsertWindow returns the window');
  Check((TProgram.DeskTop.First = W1) and ((W1.State and sfActive) <> 0), 'the window is in front and active');
  Check(MemText(3, 2, 6) = '╔═[■]', 'the frame of the window is drawn');
  Check(MemAttr(2, 3) = $1F, 'active frame color');
  Check(MemIsShadow(22, 5) and MemIsShadow(23, 11) and not MemIsShadow(21, 5) and not MemIsShadow(22, 3), 'the shadow of the window');
  W2 := App.InsertWindow(NewWin(2, 10, 6));
  Check(TProgram.DeskTop.First = W2, 'the second window is in front');
  MemKey(kbAlt1, kbAltShift);
  Pump(App);
  Check(TProgram.DeskTop.First = W1, 'Alt+1 selects window 1');
  MemKey(kbAlt2, kbAltShift);
  Pump(App);
  Check(TProgram.DeskTop.First = W2, 'Alt+2 selects window 2');
  MemKey(kbAlt9, kbAltShift);
  Pump(App);
  Check(TProgram.DeskTop.First = W2, 'Alt+9: no such window');
  Check(App.InsertWindow(nil) = nil, 'InsertWindow(nil) is nil');

  { next and previous window }
  Ev.What := evCommand; Ev.Message.Command := cmNext; Ev.Message.InfoPtr := nil;
  App.HandleEvent(Ev);
  Check((TProgram.DeskTop.First = W1) and (Ev.What = evNothing), 'cmNext selects the next window');
  Ev.What := evCommand; Ev.Message.Command := cmPrev; Ev.Message.InfoPtr := nil;
  App.HandleEvent(Ev);
  Check(TProgram.DeskTop.First = W2, 'cmPrev puts the front window behind the others');

  { tile and cascade }
  W3 := App.InsertWindow(NewWin(3, 20, 10));
  Ev.What := evCommand; Ev.Message.Command := cmTile; Ev.Message.InfoPtr := nil;
  App.HandleEvent(Ev);
  Check(Ev.What = evNothing, 'cmTile is handled');
  Ys[0] := W1.Origin.Y; Ys[1] := W2.Origin.Y; Ys[2] := W3.Origin.Y;
  for I := 0 to 1 do
    for D := I + 1 to 2 do
      if Ys[D] < Ys[I] then begin Res := Ys[I]; Ys[I] := Ys[D]; Ys[D] := Res; end;
  Check((Ys[0] = 0) and (Ys[1] = 7) and (Ys[2] = 15), 'tiled in three rows');
  Check((W1.Size.X = 60) and (W2.Size.X = 60) and (W3.Size.X = 60), 'tiled windows are as wide as the desktop');
  Check(W1.Size.Y + W2.Size.Y + W3.Size.Y = 23, 'and fill its height');
  Ev.What := evCommand; Ev.Message.Command := cmCascade; Ev.Message.InfoPtr := nil;
  App.HandleEvent(Ev);
  Check(Ev.What = evNothing, 'cmCascade is handled');
  Check((W3.Origin.X = W3.Origin.Y) and (W1.Origin.X = W1.Origin.Y) and
    (W2.Origin.X = W2.Origin.Y), 'cascaded windows are on the diagonal');
  Check(((W1.Origin.X = 0) or (W2.Origin.X = 0) or (W3.Origin.X = 0)) and
    ((W1.Origin.X = 1) or (W2.Origin.X = 1) or (W3.Origin.X = 1)) and
    ((W1.Origin.X = 2) or (W2.Origin.X = 2) or (W3.Origin.X = 2)), 'offsets 0, 1 and 2');
  Rc := W1.GetBounds;
  Check((Rc.B.X = 60) and (Rc.B.Y = 23), 'a cascaded window reaches the corner of the desktop');
  W3.Free;

  { idle, commands, timers }
  Watch := TWatch.Create(R(0, 0, 1, 1));
  Watch.EventMask := evBroadcast;
  TProgram.DeskTop.Insert(Watch);
  App.Idles := 0;
  TView.CommandSetChanged := False;
  TView.DisableCommand(cmClose);
  Check(TView.CommandSetChanged, 'disabling a command sets CommandSetChanged');
  App.Idle;
  Check((Watch.SetChanged = 1) and not TView.CommandSetChanged, 'Idle broadcasts cmCommandSetChanged once');
  App.Idle;
  Check(Watch.SetChanged = 1, 'and not again');
  TView.EnableCommand(cmClose);
  { idle is called when there is no event }
  MemClear;
  App.Idles := 0;
  App.GetEvent(Ev);
  Check((Ev.What = evNothing) and (App.Idles = 1), 'no event: Idle is called');
  Check(MemClock = 20, 'the program waited EventTimeoutMs');
  MemKey(kbF9);
  App.Idles := 0;
  App.GetEvent(Ev);
  Check((Ev.What = evKeyDown) and (App.Idles = 0), 'an event: no Idle');
  { the pending event (PutEvent) comes first }
  MemKey(kbF8);
  Ev.What := evCommand; Ev.Message.Command := cmOK; Ev.Message.InfoPtr := nil;
  App.PutEvent(Ev);
  App.GetEvent(Ev);
  Check((Ev.What = evCommand) and (Ev.Message.Command = cmOK), 'PutEvent: the pending event comes first');
  App.GetEvent(Ev);
  Check((Ev.What = evKeyDown) and (Ev.KeyDown.KeyCode = kbF8), 'then the queue');

  TProgram.EventTimeoutMs := 20;
  MemClear;
  MemClock := 1000;
  Id := App.SetTimer(100);
  I := 0;
  while (Watch.TimerFired = 0) and (I < 20) do
  begin
    App.GetEvent(Ev);
    Inc(I);
  end;
  Check(Watch.TimerFired = 1, 'a timer sends cmTimerExpired');
  Check(Watch.LastTimer = Id, 'with its id');
  Check((MemClock >= 1100) and (I = 5), 'after its time, noticed at the next Idle');
  App.GetEvent(Ev);
  Check(Watch.TimerFired = 1, 'a one-shot timer fires once');
  { with no event timeout the program sleeps exactly until the timer }
  TProgram.EventTimeoutMs := -1;
  MemClock := 5000;
  Watch.TimerFired := 0;
  Id := App.SetTimer(250);
  App.GetEvent(Ev);
  Check((MemClock = 5250) and (Watch.TimerFired = 1), 'EventTimeoutMs = -1: wait until the timer');
  Id := App.SetTimer(1000, 1000);
  App.KillTimer(Id);
  Check(True, 'KillTimer');
  TProgram.EventTimeoutMs := 20;
  Watch.Free;

  { the status line gets keys and clicks before the others }
  MemClear;
  MemKey(kbAltX, kbAltShift);
  App.GetEvent(Ev);
  Check((Ev.What = evCommand) and (Ev.Message.Command = cmQuit), 'a key of the status line becomes its command');
  MemClear;
  MemMouse(evMouseDown, 3, 24);
  MemMouse(evMouseUp, 3, 24);
  App.GetEvent(Ev);
  Check(Ev.What = evNothing, 'a click on the status line is handled by it');
  App.GetEvent(Ev);
  Check((Ev.What = evCommand) and (Ev.Message.Command = cmQuit), 'and its command comes next');

  { dialogs }
  Dlg := TDlg.Create(R(0, 0, 10, 5));
  D := 55;
  Dlg.Value := 0;
  Res := App.ExecuteDialog(Dlg, @D);
  Check(Res = cmOK, 'ExecuteDialog returns the result of the dialog');
  Check(D = 99, 'and the data of the dialog (set before, read after)');
  Check(TProgram.DeskTop.First = W2, 'the dialog is gone');

  { the screen changes }
  OnSetVideoMode := @Resize;
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evCommand; Ev.Message.Command := cmScreenChanged;
  App.PutEvent(Ev);
  App.GetEvent(Ev);
  Check(Ev.What = evNothing, 'cmScreenChanged is handled');
  Check((TScreen.ScreenWidth = 80) and (TScreen.ScreenHeight = 30), 'the screen was resized');
  Check((App.Size.X = 80) and (App.Size.Y = 30), 'the program has the new size');
  Check((TProgram.StatusLine.Origin.Y = 29) and (TProgram.StatusLine.Size.X = 80), 'the status line moved to the bottom');
  Check(MemText(29, 0, 11) = ' Alt-X Exit ', 'and is drawn there');
  Check((TProgram.DeskTop.Size.X = 80) and (TProgram.DeskTop.Size.Y = 28), 'the desktop grew');
  Check(W2.Size.X > 60, 'the windows grow with the desktop (relative grow mode)');
  OnSetVideoMode := nil;

  { TextEvent: the text of a paste in one string; the first other event is put back }
  MemClear;
  begin
    ClearEvent(TE);
    TE.What := evKeyDown; TE.KeyDown.KeyCode := 0; TE.KeyDown.ControlKeyState := kbPaste;
    TE.KeyDown.Text[0] := 'a'; TE.KeyDown.TextLength := 1;
    MemEvent(TE);
    TE.KeyDown.Text[0] := #$D0; TE.KeyDown.Text[1] := #$B6; TE.KeyDown.TextLength := 2;      { a Cyrillic letter: 2 bytes }
    MemEvent(TE);
    TE.KeyDown.Text[0] := #10; TE.KeyDown.TextLength := 1;
    MemEvent(TE);
    TE.KeyDown.Text[0] := 'z'; TE.KeyDown.TextLength := 1; TE.KeyDown.ControlKeyState := 0;  { typed, not pasted }
    MemEvent(TE);
    MemKey(kbF9);
    Check(MemPending = 5, 'TextEvent: five events are waiting');
    TE.KeyDown.Text[0] := 'x'; TE.KeyDown.TextLength := 1; TE.KeyDown.ControlKeyState := kbPaste;   { the event that the loop has just got }
    Check(TProgram.DeskTop.TextEvent(TE, TS), 'TextEvent: there is text');
    Check(TS = 'xa'#$D0#$B6#10, 'TextEvent: the text of the first event and of the pasted ones that follow, in one string');
    Check(TE.What = evNothing, 'TextEvent: the event is cleared');
    Check(MemPending = 1, 'TextEvent: the typed letter was taken to see what it is, the next event (F9) is left');
    ClearEvent(TE);
    TE.What := evCommand; TE.Message.Command := cmQuit;
    Check(not TProgram.DeskTop.TextEvent(TE, TS), 'TextEvent: no text in a command');
    MemClear;
  end;

  { Run ends with cmQuit }
  MemClear;
  MemKey(kbAltX, kbAltShift);
  App.Run;
  Check(App.EndState = cmQuit, 'Run: Alt-X quits');

  { DN: SystemColors are the palettes of the program and can be changed }
  Check(Length(SystemColors[apColor]) = PalLen(App), 'DN extensions: GetPalette is made of SystemColors');
  Save := SystemColors[apColor];
  SystemColors[apColor] := #1#2#3;
  TProgram.AppPalette := apColor;
  Check(PalLen(App) = 3, 'DN extensions: a changed SystemColors is the palette');
  SystemColors[apColor] := Save;

  App.Free;
  Check((TProgram.Application = nil) and (TProgram.DeskTop = nil) and (TProgram.StatusLine = nil) and (TProgram.MenuBar = nil), 'Done clears the variables');
  MemDone;
  Check(GetFPCHeapStatus.CurrHeapUsed = Used0, 'no memory is left behind');
  Finish;
end.
