program t_window;
{$I ../src/tvdefs.inc}
uses TvCodePg, TvGeom, TvColors, TvCell, TvEvents, TvKeys, TvDrawBuf, TvScreen, TvObjs, TvViews,
  TvWindow;
{$I testlib.inc}

function CommandsOf(const A: array of Integer): TCommandSet;
var
  I: Integer;
begin
  Result := Default(TCommandSet);
  for I := 0 to High(A) do
    Result := Result + A[I];
end;

const
  W = 40;
  H = 12;

type
  { the top view of the tests: gives the events of a queue to the views that ask
    for them (the mouse loops of the frame and of the scroll bar) and keeps the
    last event put back }
  TTop = class(TGroup)
    Queue: array[0..7] of TEvent;
    QCount, QPos: Integer;
    Pending: TEvent;
    procedure GetEvent(var Event: TEvent); override;
    procedure PutEvent(var Event: TEvent); override;
    procedure Enqueue(What: Word; X, Y: Integer);
  end;

  { a view that fills itself with one character: the background, or a framed child }
  TFill = class;
  TFill = class(TView)
    Ch: Byte;
    constructor Create(const Bounds: TRect; ACh: Char);
    procedure Draw; override;
    function GetPalette: TPalette; override;
  end;

var
  SaveCmds: TCommandSet;
  Desk: TTop;

procedure TTop.GetEvent(var Event: TEvent);
begin
  if QPos < QCount then
  begin
    Event := Queue[QPos];
    Inc(QPos);
  end
  else
    Event.What := evNothing;
end;

procedure TTop.PutEvent(var Event: TEvent);
begin
  Pending := Event;
end;

procedure TTop.Enqueue(What: Word; X, Y: Integer);
begin
  FillChar(Queue[QCount], SizeOf(TEvent), 0);
  Queue[QCount].What := What;
  Queue[QCount].Mouse.Where.X := X;
  Queue[QCount].Mouse.Where.Y := Y;
  Queue[QCount].Mouse.Buttons := mbLeftButton;
  Inc(QCount);
end;

constructor TFill.Create(const Bounds: TRect; ACh: Char);
begin
  inherited Create(Bounds);
  Ch := Ord(ACh);
end;

function HideZoom(Command: Word): Boolean;
begin
  Result := Command = cmZoom;
end;

procedure TFill.Draw;
var
  B: TDrawBuffer;
begin
  B := TDrawBuffer.Create(W);
  B.MoveChar(0, Ch, GetColor(1)[0], Size.X);
  WriteLine(0, 0, Size.X, Size.Y, B);
  B.Free;
end;

function TFill.GetPalette: TPalette;
begin
  Result := TPalette.Create(PChar(#7#0), 1);
end;

function Cell(X, Y: Integer): PScreenCell;
begin
  Result := TScreen.ScreenBuffer + (Y * TScreen.ScreenWidth + X);
end;

{ the text of a cell as UTF-8: characters stored as code page bytes are
  converted (the screen does the same when it shows them) }
function CellText(X, Y: Integer): ShortString;
var
  Bytes: array[0..7] of Byte;
  C: Byte;
begin
  Result := (Cell(X, Y)^.Character).GetText;
  if Result = #0 then
    Exit(' ');
  if Length(Result) = 1 then
  begin
    C := Byte(Result[1]);
    if (C >= $80) or (C < $20) then
    begin
      SetLength(Result, CpToUtf8(C, @Bytes[0]));
      Move(Bytes[0], Result[1], Length(Result));
    end;
  end;
end;

function Row(Y, X0, X1: Integer): ShortString;
var
  X: Integer;
begin
  Result := '';
  for X := X0 to X1 do
    Result := Result + CellText(X, Y);
end;

function AttrAt(X, Y: Integer): Byte;
begin
  Result := Byte(Cell(X, Y)^.Attribute);
end;

function R(A, B, C, D: Integer): TRect;
begin
  Result := TRect.Create(A, B, C, D);
end;

function Bit(V: TView; Mask: Word): Boolean;
begin
  Result := (V.State and Mask) = Mask;
end;

var
  Back, Boxed: TFill;
  Win, Win2: TWindow;
  Bar, HBar: TScrollBar;
  Scr: TScroller;
  SW, SL: TWindow;
  SSb, SSb2: TScrollBar;
  SSc, SSc2: TScroller;
  SM: TMemoryStream;
  I: Integer;
  Ev: TEvent;
  Min, Max: TPoint;
  Rc: TRect;
  Count: Integer;
  Before: TRect;

procedure CountViews(P: TView; Args: Pointer);
begin
  Inc(PInteger(Args)^);
end;

begin
  ScreenCreate(W, H);
  Desk := TTop.Create(R(0, 0, W, H));
  Desk.Options := 0;
  Desk.Buffer := TScreen.ScreenBuffer;
  Desk.State := sfVisible or sfSelected or sfFocused or sfModal or sfExposed;
  Desk.QCount := 0;
  Desk.QPos := 0;

  Back := TFill.Create(R(0, 0, W, H), '.');
  Desk.Insert(Back);

  { --- window and frame ---------------------------------------------------- }
  { the commands of a window are enabled while it is selected }
  TView.DisableCommands(CommandsOf([cmClose, cmZoom, cmResize, cmNext, cmPrev]));
  Check(not TView.CommandEnabled(cmClose) and not TView.CommandEnabled(cmZoom), 'window commands disabled');
  Win := TWindow.Create(R(2, 1, 22, 9), 'Hi', 1);
  Desk.Insert(Win);
  Check(TView.CommandEnabled(cmClose) and TView.CommandEnabled(cmZoom) and TView.CommandEnabled(cmResize) and
    TView.CommandEnabled(cmNext) and TView.CommandEnabled(cmPrev), 'a selected window enables its commands');
  Check(Win.Frame <> nil, 'the window has a frame');
  Check(Win.Last = TView(Win.Frame), 'the frame is the only (bottom) subview');
  Check(Bit(Win, sfSelected or sfActive), 'a new window is selected and active');
  Check(Bit(Win.Frame, sfActive), 'its frame is active');
  Check((Win.Options and (ofSelectable or ofTopSelect)) = (ofSelectable or ofTopSelect),
    'window options');
  Check((Win.State and sfShadow) <> 0, 'a window has a shadow');
  Check(Win.GrowMode = (gfGrowAll or gfGrowRel), 'window grow mode');
  Check(Row(1, 2, 21) = '╔═[■]═══ Hi ═1═[↑]═╗', 'active frame: top line with icons, title, number');
  Check(Row(2, 2, 21) = '║                  ║', 'active frame: side lines');
  Check(Row(8, 2, 21) = '└─════════════════─┘', 'active frame: bottom line with resize corners');
  Check(AttrAt(2, 1) = $09, 'active frame color');
  Check(AttrAt(5, 1) = $0A, 'close icon color');
  Check(AttrAt(11, 1) = $09, 'title color');
  Check(CellText(5, 1) = '■', 'close icon');
  Check(CellText(18, 1) = '↑', 'zoom icon');
  Check(Row(1, 0, 1) = '..', 'the background shows beside the window');

  { a framed child is boxed in by the frame }
  Boxed := TFill.Create(R(4, 2, 10, 5), 'x');
  Boxed.Options := Boxed.Options or ofFramed;
  Win.Insert(Boxed);
  Win.Frame.DrawView;   { drawing the child does not draw the frame }
  Check(Row(2, 2, 21) = '║  ┌──────┐        ║', 'framed child: line above');
  Check(Row(3, 2, 21) = '║  │xxxxxx│        ║', 'framed child: side lines');
  Check(Row(6, 2, 21) = '║  └──────┘        ║', 'framed child: line below');
  Boxed.Hide;
  Win.Frame.DrawView;
  Check(Row(2, 2, 21) = '║                  ║', 'a hidden framed child leaves no box');
  Boxed.Free;

  { --- a second window: the first becomes passive ---------------------------- }
  Win2 := TWindow.Create(R(10, 4, 30, 11), 'Two', 2);
  Desk.Insert(Win2);
  Check(Desk.First = TView(Win2), 'the new window is in front');
  Check(Bit(Win2, sfActive), 'the new window is active (the background is not selectable)');
  Check(not Bit(Win, sfActive) and not Bit(Win.Frame, sfActive), 'the first window is passive');
  Check(Row(1, 2, 21) = '┌─────── Hi ─1─────┐', 'passive frame: no icons, single lines');
  Check(AttrAt(2, 1) = $08, 'passive frame color');
  Check(Row(4, 10, 29) = '╔═[■]══ Two ═2═[↑]═╗', 'second window frame');
  Check(Row(8, 2, 10) = '└───────║', 'the second window covers the first one');
  Win.Select;
  Check(Desk.First = TView(Win), 'Select brings a window (ofTopSelect) to the front');
  Check(Bit(Win, sfActive) and not Bit(Win2, sfActive), 'and makes it active');
  Check(Row(10, 10, 29) = '└──────────────────┘', 'the second window is passive now (bottom line)');
  Ev.What := evBroadcast;
  Ev.Message.Command := cmSelectWindowNum;
  Ev.Message.InfoInt := 2;
  Desk.HandleEvent(Ev);
  Check(Desk.First = TView(Win2), 'cmSelectWindowNum selects the window with that number');
  Check(Ev.What = evNothing, 'and the event is handled');

  { --- zoom -------------------------------------------------------------------- }
  Win2.SizeLimits(Min, Max);
  Check((Min.X = 16) and (Min.Y = 6) and (Max.X = W) and (Max.Y = H), 'window size limits');
  Before := Win2.GetBounds;
  Win2.Zoom;
  Rc := Win2.GetBounds;
  Check((Rc.A.X = 0) and (Rc.A.Y = 0) and (Rc.B.X = W) and (Rc.B.Y = H), 'Zoom: the whole desktop');
  Check((Win2.ZoomRect.A.X = 10) and (Win2.ZoomRect.A.Y = 4), 'the old bounds are remembered');
  Check(CellText(W - 5 + 1, 0) = '↕', 'zoomed: the unzoom icon');
  Win2.Zoom;
  Rc := Win2.GetBounds;
  Check((Rc.A.X = Before.A.X) and (Rc.A.Y = Before.A.Y) and (Rc.B.X = Before.B.X) and
    (Rc.B.Y = Before.B.Y), 'Zoom again: the old bounds');

  { --- frame, mouse -------------------------------------------------------------- }
  { close icon: pressed and released over it }
  Desk.QCount := 0; Desk.QPos := 0;
  Desk.Pending.What := evNothing;
  Desk.Enqueue(evMouseUp, 13, 4);
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evMouseDown;
  Ev.Mouse.Where.X := 13; Ev.Mouse.Where.Y := 4;
  Ev.Mouse.Buttons := mbLeftButton;
  Desk.HandleEvent(Ev);
  Check((Desk.Pending.What = evCommand) and (Desk.Pending.Message.Command = cmClose) and
    (Desk.Pending.Message.InfoPtr = Pointer(Win2)), 'click on the close icon puts cmClose for the window');
  Check(Ev.What = evNothing, 'the click is handled');
  { pressed over it, released elsewhere: nothing }
  Desk.QCount := 0; Desk.QPos := 0;
  Desk.Pending.What := evNothing;
  Desk.Enqueue(evMouseUp, 20, 8);
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evMouseDown;
  Ev.Mouse.Where.X := 13; Ev.Mouse.Where.Y := 4;
  Ev.Mouse.Buttons := mbLeftButton;
  Desk.HandleEvent(Ev);
  Check(Desk.Pending.What = evNothing, 'released elsewhere: no cmClose');
  { double click on the title: zoom }
  Desk.QCount := 0; Desk.QPos := 0;
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evMouseDown;
  Ev.Mouse.Where.X := 20; Ev.Mouse.Where.Y := 4;
  Ev.Mouse.Buttons := mbLeftButton;
  Ev.Mouse.EventFlags := meDoubleClick;
  Desk.HandleEvent(Ev);
  Check((Desk.Pending.What = evCommand) and (Desk.Pending.Message.Command = cmZoom) and
    (Desk.Pending.Message.InfoPtr = Pointer(Win2)), 'double click on the top line puts cmZoom');


  { --- scroll bar and scroller ----------------------------------------------------- }
  Win.Select;
  Bar := Win.StandardScrollBar(sbVertical or sbHandleKeyboard);
  HBar := Win.StandardScrollBar(sbHorizontal);
  Rc := Bar.GetBounds;
  Check((Rc.A.X = 19) and (Rc.A.Y = 1) and (Rc.B.X = 20) and (Rc.B.Y = 7), 'vertical scroll bar bounds');
  Rc := HBar.GetBounds;
  Check((Rc.A.X = 2) and (Rc.A.Y = 7) and (Rc.B.X = 18) and (Rc.B.Y = 8), 'horizontal scroll bar bounds');
  Check((Bar.Options and ofPostProcess) <> 0, 'sbHandleKeyboard: post-process');
  Check((HBar.Options and ofPostProcess) = 0, 'otherwise not');
  Check(Bar.Chars[3] = $FE, 'thumb character');
  Check(Bar.Chars[0] = $1E, 'vertical bar: up arrow');
  Check(HBar.Chars[0] = $11, 'horizontal bar: left arrow');
  Check(CellText(21, 2) = '▲', 'drawn: up arrow');
  Check(CellText(21, 7) = '▼', 'drawn: down arrow');
  Check(CellText(21, 3) = '▓', 'drawn: an empty range has no page area');
  Bar.SetParams(3, 0, 10, 4, 1);
  Check((Bar.Value = 3) and (Bar.MaxVal = 10) and (Bar.PgStep = 4), 'SetParams');
  Check(Bar.GetPos = 2, 'thumb position');
  Check(CellText(21, 3) = '▒', 'drawn: page area');
  Check(CellText(21, 4) = '■', 'drawn: thumb');
  Bar.SetValue(99);
  Check(Bar.Value = 10, 'the value is limited to the maximum');
  Bar.SetRange(0, 5);
  Check(Bar.Value = 5, 'a smaller range limits the value');
  Bar.SetParams(0, 0, 10, 4, 1);
  Check((Bar.ScrollStep(sbDownArrow) = 1) and (Bar.ScrollStep(sbUpArrow) = -1) and
    (Bar.ScrollStep(sbPageDown) = 4) and (Bar.ScrollStep(sbPageUp) = -4), 'ScrollStep');
  Check((Bar.Step = -4) and not Bar.ForceScroll, 'DN extensions: Step is the last step of ScrollStep, ForceScroll is off');
  TView.EnableCommands(CommandsOf([cmZoom]));
  Bar.DisableCommands(CommandsOf([cmZoom]));
  Check(not TView.CurCommandSet.Has(cmZoom), 'DN extensions: DisableCommands as a method of a view');
  Bar.EnableCommand(cmZoom);
  Check(TView.CurCommandSet.Has(cmZoom), 'DN extensions: EnableCommand as a method of a view');
  Check(Bar.MenuEnabled(cmZoom) and Bar.MenuEnabled(3000), 'DN extensions: MenuEnabled follows the command set (above 255 always)');
  CommandHiddenHook := @HideZoom;
  Check(not Bar.MenuEnabled(cmZoom) and Bar.MenuEnabled(cmClose), 'DN extensions: a hidden command is not enabled');
  CommandHiddenHook := nil;
  Bar.GetCommands(SaveCmds);
  Bar.SetCommands(CommandsOf([cmClose]));
  Check(TView.CurCommandSet.Has(cmClose) and not TView.CurCommandSet.Has(cmZoom), 'DN extensions: SetCommands as a method of a view');
  Bar.SetCommands(SaveCmds);
  Check(TView.CurCommandSet.Has(cmZoom), 'DN extensions: GetCommands saved the set');

  { keys }
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evKeyDown; Ev.KeyDown.KeyCode := kbDown;
  Bar.HandleEvent(Ev);
  Check((Bar.Value = 1) and (Ev.What = evNothing), 'Down: one step');
  Ev.What := evKeyDown; Ev.KeyDown.KeyCode := kbPgDn;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 5, 'PgDn: one page');
  Ev.What := evKeyDown; Ev.KeyDown.KeyCode := kbCtrlPgDn;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 10, 'Ctrl+PgDn: the end');
  Ev.What := evKeyDown; Ev.KeyDown.KeyCode := kbUp;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 9, 'Up: one step back');
  Ev.What := evKeyDown; Ev.KeyDown.KeyCode := kbCtrlPgUp;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 0, 'Ctrl+PgUp: the start');
  Ev.What := evKeyDown; Ev.KeyDown.KeyCode := kbEnter;
  Bar.HandleEvent(Ev);
  Check(Ev.What = evKeyDown, 'other keys are not handled');
  { wheel }
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evMouseWheel; Ev.Mouse.Wheel := mwDown;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 3, 'wheel down: three steps');
  Ev.What := evMouseWheel; Ev.Mouse.Wheel := mwLeft;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 3, 'a vertical bar ignores a horizontal wheel');
  { mouse: the down arrow (global 21,7) }
  Desk.QCount := 0; Desk.QPos := 0;
  Desk.Enqueue(evMouseUp, 21, 7);
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evMouseDown; Ev.Mouse.Where.X := 21; Ev.Mouse.Where.Y := 7; Ev.Mouse.Buttons := mbLeftButton;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 4, 'click on the down arrow: one step');
  { dragging the thumb to the last row before the arrow sets the maximum }
  Bar.SetParams(0, 0, 10, 4, 1);
  Desk.QCount := 0; Desk.QPos := 0;
  Desk.Enqueue(evMouseMove, 21, 6);
  Desk.Enqueue(evMouseUp, 21, 6);
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evMouseDown; Ev.Mouse.Where.X := 21; Ev.Mouse.Where.Y := 3; Ev.Mouse.Buttons := mbLeftButton;
  Bar.HandleEvent(Ev);
  Check(Bar.Value = 10, 'dragging the thumb to the end');

  { the scroller }
  Bar.SetParams(0, 0, 10, 4, 1);
  Rc := Win.GetExtent;
  Rc.Grow(-1, -1);
  Scr := TScroller.Create(Rc, HBar, Bar);
  Win.Insert(Scr);
  Scr.SetLimit(100, 50);
  Check((Bar.MaxVal = 50 - Scr.Size.Y) and (Bar.PgStep = Scr.Size.Y - 1), 'SetLimit sets the vertical bar');
  Check((HBar.MaxVal = 100 - Scr.Size.X) and (HBar.PgStep = Scr.Size.X - 1), 'and the horizontal bar');
  Scr.ScrollTo(5, 7);
  Check((Scr.Delta.X = 5) and (Scr.Delta.Y = 7), 'ScrollTo moves the scroller');
  Check((Bar.Value = 7) and (HBar.Value = 5), 'and the bars');
  Bar.SetValue(12);
  Check(Scr.Delta.Y = 12, 'a bar moves the scroller (cmScrollBarChanged)');
  Win2.Select;
  Win.Select;
  Check(Bit(Scr, sfSelected or sfActive) and Bit(Bar, sfVisible) and Bit(HBar, sfVisible),
    'the bars are shown while the scroller is selected and active');
  Win2.Select;
  Check(not Bit(Win, sfActive) and not Bit(Bar, sfVisible) and not Bit(HBar, sfVisible),
    'and hidden when its window is passive');
  Win.Select;
  Check(Bit(Bar, sfVisible), 'shown again');

  { --- Done and Close ------------------------------------------------------------- }
  Count := 0;
  Desk.ForEach(@CountViews, @Count);
  Check(Count = 3, 'the desk holds the background and two windows');
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evCommand;
  Ev.Message.Command := cmClose;
  Win.HandleEvent(Ev);
  Count := 0;
  Desk.ForEach(@CountViews, @Count);
  Check(Count = 2, 'cmClose closes the window');
  Check(Desk.First = TView(Win2), 'and it leaves the other window');
  Check(Row(1, 2, 5) = '....', 'the background shows where it was');
  Check(Bit(Win2, sfActive), 'the other window becomes active');

  { cmClose with the pointer of another window is not for this one }
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evCommand;
  Ev.Message.Command := cmClose;
  Ev.Message.InfoPtr := Back;
  Win2.HandleEvent(Ev);
  Count := 0;
  Desk.ForEach(@CountViews, @Count);
  Check((Count = 2) and (Ev.What = evCommand), 'cmClose for another view is ignored');
  { a modal window turns cmClose into cmCancel }
  Win2.State := Win2.State or sfModal;
  Desk.Pending.What := evNothing;
  FillChar(Ev, SizeOf(Ev), 0);
  Ev.What := evCommand;
  Ev.Message.Command := cmClose;
  Win2.HandleEvent(Ev);
  Check((Desk.Pending.What = evCommand) and (Desk.Pending.Message.Command = cmCancel), 'modal: cmClose becomes cmCancel');
  Win2.State := Win2.State and not sfModal;

  Win2.Free;
  Count := 0;
  Desk.ForEach(@CountViews, @Count);
  Check(Count = 1, 'Dispose removes a window from the desk');
  Desk.Free;
  { streams: a window with a frame, a scroll bar and a scroller that points to it }
  RegisterType(RView);
  RegisterType(RGroup);
  RegisterType(RFrame);
  RegisterType(RScrollBar);
  RegisterType(RScroller);
  RegisterType(RWindow);
  SW := TWindow.Create(R(0, 0, 30, 10), 'Hello', 3);
  SW.Flags := SW.Flags and not wfZoom;
  SSb := SW.StandardScrollBar(sbVertical or sbHandleKeyboard);
  SSc := TScroller.Create(R(1, 1, 28, 9), nil, SSb);
  SSc.Limit.X := 100;
  SSc.Limit.Y := 50;
  SSb.SetParams(3, 0, 40, 5, 1);
  SW.Insert(SSc);
  SM := TMemoryStream.Create(0, 512);
  SM.Put(TStreamable(Pointer(SW)));
  Check(SM.Status = stOk, 'a window is stored');
  SM.Seek(0);
  SL := TWindow(Pointer(SM.Get));
  Check((SL <> nil) and (SM.Status = stOk), 'a window is loaded');
  Check((SL.Title^ = 'Hello') and (SL.Number = 3) and (SL.Flags and wfZoom = 0), 'the title, the number and the flags');
  Check((SL.Frame <> nil) and (SL.Frame.Owner = TGroup(SL)), 'the frame is a view of the window');
  SSc2 := nil;
  SSb2 := nil;
  for I := 1 to 8 do
    if SL.At(I) <> nil then
    begin
      if SL.At(I) is TScroller then SSc2 := TScroller(SL.At(I));
      if SL.At(I) is TScrollBar then SSb2 := TScrollBar(SL.At(I));
    end;
  Check((SSc2 <> nil) and (SSb2 <> nil), 'the scroller and the scroll bar are loaded');
  Check(SSc2.VScrollBar = TScrollBar(SSb2), 'the scroller points to the loaded scroll bar');
  Check((SSb2.Value = 3) and (SSb2.MaxVal = 40) and (SSb2.PgStep = 5), 'the values of the scroll bar');
  Check((SSc2.Limit.X = 100) and (SSc2.Limit.Y = 50), 'the limit of the scroller');
  SL.Free;
  SW.Free;
  SM.Free;

  Finish;
end.
